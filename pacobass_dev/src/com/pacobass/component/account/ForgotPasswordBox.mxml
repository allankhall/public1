<?xml version="1.0" encoding="utf-8"?>
<mx:VBox 
	
	xmlns:mx="library://ns.adobe.com/flex/mx"
	xmlns:fx="http://ns.adobe.com/mxml/2009" 
	width="100%" height="100%"
		creationComplete = "init()" hide = "resetForm()">
	<fx:Metadata>
		[Event(name="forgotPasswordHelper", type = "com.pacobass.event.ForgotPasswordHelperEvent")]
	</fx:Metadata>
	<fx:Script>
 		<![CDATA[
 			import com.pacobass.event.ForgotPasswordHelperEvent;
  			import com.pacobass.util.Constant;		
 	
			import mx.controls.Alert; 		
 			import mx.events.ValidationResultEvent;
 			import mx.rpc.events.FaultEvent;
 			import mx.rpc.events.ResultEvent;
 			import mx.validators.EmailValidator;
 		
 		
  			private var mailValidator:EmailValidator;   //email input validator
  			private var emailValid:Boolean; //global boolean for email validation 
  		
  			/**
			* null out errors and inputs and disable submit
		 	* @public
	     	*/
 			public function resetForm():void{
 				emailInput.text = "";
 				emailInput.errorString = ""
 				forgotPasswordButton.enabled = false;
 			} 		
 		
 			/**
 		 	* setup form validators and disable submit button
 		 	* @private
 	     	*/
 			private function init():void{
 				mailValidator = new EmailValidator();	
 				mailValidator.source = emailInput;
				mailValidator.property = "text";
 				emailValid = new Boolean();
 				forgotPasswordButton.enabled = false;
 			}
 		
 			/**
 		 	* validate form and set submit button
 		 	* @private
 		 	*/
 			private function enableSubmit():void{
 				emailValid = (mailValidator.validate().type == ValidationResultEvent.VALID);
 				forgotPasswordButton.enabled = emailValid;
 			}
 		
 			/**
 		 	* make service call to email password
 		 	* @private
 		 	*/
 			private function callZend():void{
 				zendAMF.sendPassword(emailInput.text);
 			}
 		

 			/**
 		 	* null out form and dispatch cancel event
 		 	* to parent 
 		 	* @private
		 	*/
 			private function cancel(button:String):void{
				resetForm();
				dispatchEvent(new ForgotPasswordHelperEvent(button));
			}	
		
			/**
		 	* alert on error from service call
			 * @private
	     	*/
			private function onError(err:FaultEvent):void {
				Alert.show(Constant.GENERAL_ERROR);
			}	
		
			/**
		 	* respond to service call
		 	* @private
		 	* @param event - bool: true if email registered or false otherwise
		 	*/
			private function onSendPasswordResult(event:ResultEvent):void{
				var result:Boolean  = event.result as Boolean;
				if(result){
					Alert.show("your password has been sent");
					dispatchEvent(new ForgotPasswordHelperEvent(Constant.PASSWORD_SENT));
				}else{
					Alert.show("We were unable to process your request");
					dispatchEvent(new ForgotPasswordHelperEvent(Constant.PASSWORD_NOT_SENT));
				}
			}
 		
 		]]>
	</fx:Script>
	<fx:Declarations>
		<mx:RemoteObject 
			id="zendAMF" 
			destination="zend" 
			showBusyCursor="true" 
			source="test_class">
			<mx:method 
				name="sendPassword" 
				result="onSendPasswordResult(event)"  
				fault="onError(event)">
				<mx:arguments>
					<email>
						{null}
					</email>
				</mx:arguments>
			</mx:method>
		</mx:RemoteObject>
		
	</fx:Declarations>
	<mx:Label text = "FORGOT PASSWORD"/>
	<mx:Form id = "ForgotPasswordForm">
		<mx:FormItem label = "email : ">
 			<mx:TextInput 
 					id = "emailInput"
 					change = "enableSubmit()" />
 		</mx:FormItem>
 		<mx:HBox>
 			<mx:Button
 					id = "forgotPasswordButton"
 					label = "submit"
 					click = "callZend()" />
 			<mx:Button
 					id = "cancelButton"
 					label = "cancel"
 					click = 'cancel(Constant.CANCEL_LINK)' />
 		</mx:HBox>
	</mx:Form>

</mx:VBox>
